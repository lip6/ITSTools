





channel plt_belt_0, plt_table_0, plt_press_0, plt_deposit_0;
channel plt_belt_1, plt_table_1, plt_press_1, plt_deposit_1;

channel new_plate, sen, read, put_table, get_table, put_press, get_press, put_deposit;

byte done = 0;



process Plate_0 {
state new, wait_belt, wait_table, wait_press, wait_deposit;
init new;
trans
 new -> wait_belt { sync new_plate!0; },
 wait_belt -> wait_table { sync plt_belt_0!;},
 wait_table -> wait_press { sync plt_table_0!; },
 wait_press -> wait_deposit { sync plt_press_0!; },
 wait_deposit -> new { guard done <5; sync plt_deposit_0!; effect done = done +1; };

}
process Plate_1 {
state new, wait_belt, wait_table, wait_press, wait_deposit;
init new;
trans
 new -> wait_belt { sync new_plate!1; },
 wait_belt -> wait_table { sync plt_belt_1!;},
 wait_table -> wait_press { sync plt_table_1!; },
 wait_press -> wait_deposit { sync plt_press_1!; },
 wait_deposit -> new { guard done <5; sync plt_deposit_1!; effect done = done +1; };

}


process Belt {
byte k;
state wait, got_new, q1, q2 ;
init wait;
trans
 wait -> got_new { sync new_plate?k; },
 got_new -> q1 { sync sen!; },
 q1 -> q2 { guard k==0; sync plt_belt_0?; },
q1 -> q2 { guard k==1; sync plt_belt_1?; },

 q2 -> wait { sync put_table!k; };
}

process Sensor {
byte count=0;
state q;
init q;
trans
 q -> q { sync sen?; effect count = count+1;},
 q -> q { guard count >0; sync read!; effect count = count -1; };
}

process Table {
byte k;
state down_empty, down_full, up_empty, up_full;
init down_empty;
trans 
 down_empty -> down_full { sync put_table?k; },
  down_full -> up_full { guard k==0; sync plt_table_0?; },
 down_full -> up_full { guard k==1; sync plt_table_1?; },

 up_full -> up_empty { sync get_table!k; },
 up_empty -> down_empty {};
}

process Press {
byte k;
state wait, pressing, done;
init wait;
trans
 wait -> pressing { sync put_press?k; },
  pressing -> done { guard k==0; sync plt_press_0?; },
 pressing -> done { guard k==1; sync plt_press_1?; },

 done -> wait {sync get_press!k; };
}

process Deposit {
byte k;
state wait, loaded;
init wait;
trans
  loaded -> wait { guard k==0; sync plt_deposit_0?; },
 loaded -> wait { guard k==1; sync plt_deposit_1?; },

 wait -> loaded { sync put_deposit?k; };
}

process Robot {
byte at_press=0, at_table=0;
byte A = 255, B = 255;
state Atable, Bpress, wait, Apress, Bdeposit;
init wait;
trans
 //pohyb smerem k Atable ma prioritu (aby mi to nemohlo cyklit)
 Atable -> Atable { guard at_table==1 && A==255; sync get_table?A; effect at_table=0;},
 Atable -> Bpress { guard A !=255; },

 Bpress -> Bpress { guard B==255 && at_press==1; sync get_press?B; effect at_press=0; },
 Bpress -> Atable { guard A==255 && at_table==1;},
 Bpress -> wait { guard (A!=255 && at_press==0) || (B!=255 && (A!=255 || at_table==0)); },

 wait -> Bpress { guard (B==255 && at_press==1) || (A==255 && at_table==1);},
 wait -> wait { guard at_table==0; sync read?; effect at_table = 1;},
 wait -> Apress { guard (A!=255 && at_press==0) || B!=255; }, 

 Apress -> Apress { guard A!=255 && at_press==0; sync put_press!A; effect A=255, at_press=1;},
 Apress -> Bdeposit { guard B!=255; },
 Apress -> wait { guard (A==255 || at_press==1) && B==255; },

 Bdeposit -> Bdeposit { guard B!=255; sync put_deposit!B; effect B=255; },
 Bdeposit -> Apress { guard B==255; };

}

system async;